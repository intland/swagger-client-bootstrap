import groovy.json.JsonBuilder
import groovy.json.JsonSlurper
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.openapitools', name: 'openapi-generator-gradle-plugin', version: '5.2.1'
    }
}

plugins {
    id 'java'
}

allprojects {
    apply plugin: 'java'

    repositories {
            mavenCentral()
    }
    dependencies {
            compile 'javax.annotation:javax.annotation-api:1.3.2'
    }
}

group 'com.intland.codebeamer'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

def codeBeamerBaseUrl = "http://localhost:8080/cb"
// Until codeBeamer 9.5
// def codeBeamerSwaggerJsonUrl = "$codeBeamerBaseUrl/v2/swagger.json"
// From codeBeamer 10.0
// def codeBeamerSwaggerJsonUrl = "$codeBeamerBaseUrl/api/docs/v3/api-docs"
// From codeBeamer 10.1
def codeBeamerSwaggerJsonUrl = "$codeBeamerBaseUrl/api-docs/v3.json"

def swaggerJsonPath = "$projectDir/swagger_api.json"
def swaggerBuildDir = "$buildDir/swagger"
def swaggerClientDir = "$projectDir/swagger-client-sdk"

task cleanSwaggerClient{
    doLast {
        delete swaggerBuildDir
        delete swaggerClientDir
    }
}

task generateSwaggerClient(type: GenerateTask){
    group = "Swagger"
    description = "Generate java swagger client"

    dependsOn 'cleanSwaggerClient'

    generatorName = "java"
    inputSpec = swaggerJsonPath
    modelPackage = "com.intland.swagger.client.model"
    apiPackage = "com.intland.swagger.client.api"
    invokerPackage = "com.intland.swagger.client"
    packageName = "com.intland.swagger.client"

    generateApiTests = false
    generateModelTests = false

    outputDir = swaggerBuildDir
    configOptions = [
            dateLibrary: "legacy",
            systemProperties: "-classpath=" + sourceSets.main.compileClasspath
    ]

    doLast {
        copy {
            from swaggerBuildDir
            into swaggerClientDir
        }
    }
}

task downloadSwaggerJson {
    doLast {
        def swaggerJson = new File(swaggerJsonPath)
        new URL(codeBeamerSwaggerJsonUrl).withInputStream{ i -> swaggerJson.withOutputStream{ it << i }}
        def parsedJson = new JsonBuilder(new JsonSlurper().parseText(swaggerJson.text))
        swaggerJson.text = parsedJson.toPrettyString()
    }
}
